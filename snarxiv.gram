top ::= <paper>

# by David Simmons-Duffin (http://www.physics.harvard.edu/~davidsd)
# March 2010
#
# This grammar is free from context, and also free for you to use
# however you like, although it's probably not a good idea to try
# actually submitting any of these to the arXiv.  Feel free to suggest
# improvements or additions, particularly famous physicists or physics
# concepts with funny names that I forgot.
#
# The code grew organically over several hours, so it may be poorly
# organized, incomplete, and inconsistent.  Hopefully the output
# reflects that.

######## Numbers ########

zdigit ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
nzdigit ::= 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
smallinteger ::= <nzdigit> | <nzdigit><zdigit> | <nzdigit><zdigit>
n ::= n | m | <nzdigit> 

######## Basic Algebra ########

ring ::= \Z | \Q | \R | \C | \mathbb{H}

group ::= <liegroup> | <discretegroup>
liegroup ::= SU(<n>) | Sp(<n>) | SO(<n>) | G_2 | F_4 | E_6 | E_7 | E_8 |
          Spin(<n>)
discretegroup ::= \Z | \Z_<n> | \Z^<n> | Hom(<ring>,<ring>) | H^<n>(<mathspace>,<ring>)
                | H_<n>(<mathspace>,<ring>) | Ext^<n>(<ring>,<ring>) | M_<n>(<ring>) | SL_<n>(<ring>)
                | Dih_<n>
groupaction ::= orbifold | quotient

######## Spaces ########

space ::= <pluralspace> | <singspace> | <mathspace>

singspace ::= a <spacetype> | a <spaceadj> <spacetype> | <properspacename> | <spaceadj> <properspacename>
            | <mathspace> | <mathspace>
            | a <bundletype> bundle over <space> | <singspace> fibered over <singspace>
            | the moduli space of <pluralspace> | a <spacetype> <spaceproperty>
            | the <spacepart> of <space> | a <group> <groupaction> of <singspace>
            | the near horizon geometry of <singspace>
pluralspace ::= <spacetype>s | <spaceadj> <spacetype>s | <n> copies of <mathspace> |
                <pluralspace> fibered over <space> |
	        <spacetype>s <spaceproperty> |
                <bundletype> bundles over <space> | moduli spaces of <pluralspace> | <group> <groupaction>s of <pluralspace>

spaceadj ::= <spaceadj> <spaceadj> | warped | squashed | non-compact | compact |
             hyper-Kahler | Kahler | exotic | projective | noncommutative | fuzzy |
             elliptically-fibered | spin | hyperbolic | Einstein | Ricci-flat | Euclidean |
             Minkowskian | planar | harmonic | symplectic | ALE | ALF
spaceproperty ::= of <group> holonomy | with <mathadj> <mathobj>
bundletype ::= <group> | line | affine | <mathspace>
spacetype ::= Calabi-Yau <n>-fold| Riemann surface| manifold| <n>-fold| <n>-manifold|
              symmetric space| K3| del-Pezzo| Klebanov-Strassler background| RS1 background|
              lens space| Hirzebruch surface| Enriques surface| rational surface| ALE fibration|
              ALF space| linear dilaton background| Atiyah-Hitchen manifold

generalspacetype ::= surface | line | hyperplane | hypersurface

properspacename ::= Anti de Sitter Space | de Sitter Space | Taub-NUT Space | superspace

mathspace ::= AdS_<n> | S^<n> | R^<n> | CY_<n> | C^<n>
            | dS_<n> | T^<n> | <mathspace> x <mathspace> | P^<n>

spacepart ::= boundary | conformal boundary | null future | horizon | NUT

######## More Mathematics ########

mapping ::=
    function | mapping | homomorphism | homeomorphism | isomorphism
    | surjective <mapping> | injective <mapping> | holomorphism
    | biholomorphism | isometry | symplectomorphism

mathadj ::= trivial | nontrivial | zero | nonzero | general | discrete | abelian
          | non-abelian | equivariant | <symmetry> symmetric
mathobj ::= fundamental group | cohomology | homology | torsion | monodromy
          | spin structure | dimension | complex structure | flux | B-field
          | H-flux | groupoid | line bundle | principal bundle | connection
          | metric | kahler form | hyperkahler structure

representation ::= adjoint| symmetric tensor| antisymmetric tensor| singlet| doublet| triplet

######## Theories ########

theory ::= <singtheory> | <pluraltheory>
singtheory ::= <singqft> <theorymodifier> | <singstringtheory> <theorymodifier>
pluraltheory ::= <pluralqft> <theorymodifier> | <pluralstringtheory> <theorymodifier>

theorymodifier ::= || <compactified> on <space> | deformed by <operator>s | on <space>
                   | <near> <theoryobj>
compactified ::= living | compactified | dimensionally reduced | supported
near ::= in the presence of | near | surrounded by | far from

qft ::= <singqft> | <singqft> | <singqft> | <pluralqft>
singqft ::= <properqft> | <qftadj> <properqft> | <properqft> <qftproperty>
    | a <qftadj> <genericqft>
pluralqft ::= <qftadj> <genericqft>s

qftadj ::= | <qftadj> <qftadj> | supersymmetric | N=<nzdigit> | adjoint | superconformal
           | conformal | extremal | chiral | topological | <n>-dimensional | twisted
           | topologically twisted | deformed | perturbative | nonperturbative | Toda | WZW
           | invertible
qftproperty ::= with <qftobj>

qftobj ::= a <operator> | <operator>s | <mathadj> superpotential | <mathadj> kahler potential
           | <representation> <field>s | a <representation> <field>
           | gauge group <liegroup> | a <mathadj> deformation | a <optype> defect

genericqft ::= QFT| CFT| Matrix Model| TQFT| <theorytype> Theory| NCFT
theorytype ::= Effective Field | Quantum Field | Conformal Field | Soft-Collinear Effective | Topological Field
             | Heavy Quark Effective | low-energy Effective | String | Yang-Mills | Chern-Simons | gauge
             | Liouville

properqft ::= QCD | QED | supergravity | unparticle physics | QED_3 | QCD_3 | GR | JT gravity
            | Einstein gravity | General relativity | Jackiw-Teitelboim gravity | N=<n> supergravity

stringtheory ::= <singstringtheory> | <pluralstringtheory>
singstringtheory ::= String Theory | F-Theory | M-Theory | Heterotic string theory
                   | Topological String Theory | type IIA | type IIB
pluralstringtheory ::= String theories | Heterotic strings | type IIA strings | type IIB strings
                     | type I strings | topological strings | bosonic strings

######## Physics Objects ########

theoryobj ::= <singtheoryobj> | <pluraltheoryobj>

singtheoryobj ::= a <bhadj> black hole | a <singularityadj> singularity
                | a <branetype> brane <braneaction>
                | a stack of <branetype> branes <braneaction> | a <generalspacetype> defect | an instanton
                | an orientifold plane | a <branetype> instanton | a <branetype> brane probe
                | a firewall

pluraltheoryobj ::=  <bhadj> black holes | <singularityadj> singularities
                  | <branetype> branes <braneaction> | <generalspacetype> defects
                  | orientifold planes | <branetype> instantons | instantons
                  | firewalls

bhadj ::= orientifold | BTZ | Kerr | Reisner-Nordstrom | small | large
        | Schwarzschild | <branetype> brane | massive | extremal | <n>-dimensional
        | <bhadj> <bhadj>

singularityadj ::= A_<n> | B_<n> | C_<n> | D_<n> | E_6 | E_7 | E_8 | G_2 | F_4 | conifold
          | conical | ADE | orbifold | du Val | Kleinian | rational double-point | canonical
          | exceptional | <physicist>

branetype ::= NS5 | D<nzdigit> | (p,q) 7- | (p,q) | noncommutative | black | fractional D<nzdigit>
            | special lagrangian | canonical co-isotropic | holomorphic | A-type | B-type
braneaction ::= | wrapping a <mathspace> | wrapped on <singspace>

operator ::= <optype> operator| Chern-Simons term| <optype> F-term| Wilson line| 't Hooft line|
             <generalspacetype> operator| <optype> D-term
optype ::= primary | quasi-primary | marginal | relevant | irrelevant | four-quark | multi-fermion
         | loop | local | nonlocal | BPS | light-ray | defect | weight-shifting | bounded | unbounded
         | line | surface | half-BPS | 1/<n>-BPS | chiral | non-chiral | Schur | heavy | light
         | higher-spin | continuous-spin

field ::= boson| fermion| gauge-field| <n>-form| scalar

experiment ::= the LHC | SNO | DAMA | ATLAS | CDMS | Bicep II | the Tevatron | LIGO
             | future e+e- colliders | the Event-Horizon telescope

objectplace ::= at the center of the galaxy | in our solar system | on the surface of the sun
              | at the edge of our universe | in the CMB | at <experiment>
              | in the interstellar medium
              | in the early universe | during inflation | after reheating | at the GUT scale
              | at the weak scale | at $\Lambda_{QCD}$ | at the intermediate scale | at the Planck scale
              | at the <horizontype> | at <n> loops | to all orders

horizontype ::= de-Sitter horizon | event horizon | apparent horizon | Poincare horizin
              | ergosphere | ergoregion | photon sphere

######## Model ########

model ::= <singmodel> | <pluralmodel>

singmodel ::= a model of <physsubject> | a model for <physsubject> | a <physadj> model <modelmodifier>
            | the <propermodel> | the <physadj> <propermodel> | <physadj> <generalmodel>
            | <inflationadj> inflation | <generalmodel> | <generalmodel> | <physicist> <generalmodel>

pluralmodel ::= models of <physsubject> | <physadj> models <modelmodifier> | models of <particle>s

modelmodifier ::= | of <physsubject> | for <physsubject> | with <particle>s
propermodel ::= Standard Model | MSSM | <nnnn>MSSM | Thirring Model | Ising Model
              | XXZ Model | O(<n>) Model | <physicist> Model | Landau-Ginzburg Model
              | A-model | B-model | SYK Model | SUSY SYK Model | Gross-Neveu Model
              | CP<nzdigit> Model | Schwartzian Theory | BF Theory | Tricritical Ising Model
              | c=1 Matrix Model
nnnn ::= N | N<nnnn>

generalmodel ::= gravity | general relativity | RS1 | RS2 | technicolor
               | gauge mediation | anomaly mediation | <properqft>
               | <dynadjective> mechanics | <dynadjective> dynamics | hydrodynamics
               | thermodynamics | unparticle physics

dynadjective ::= quantum | <physicist> | <physadj>

######## Adjectives ########

physadj ::= <physadj> <physadj> | non-<physadj> 
          | <nondescriptivephysadj> | <descriptivephysadj>
          | <nondescriptivephysadj> | <descriptivephysadj>
          | <nondescriptivephysadj> | <descriptivephysadj>
          | <nondescriptivephysadj> | <descriptivephysadj>
          | <nondescriptivephysadj> | <descriptivephysadj>
          | <nondescriptivephysadj> | <descriptivephysadj>
          | <nondescriptivephysadj> | <descriptivephysadj>

nondescriptivephysadj ::=
             seesaw | curvaton | hybrid | quantum | loop | cosmon
           | scalar | <particle> | <physsubject> | isocurvature | <branetype> brane
           | condensate | three-fluid | multi-field | variable mass
           | particle | matrix | lattice | inflaton | bulk | boundary | halo
           | braneworld | GUT | <liegroup> | scalar field | RS 
           | flavor | Landau-Ginzburg | Planck | <physicist> | left-right
           | large-N | parent | QCD | QED | BPS | unparticle | high-scale | low-scale
           | large mass

descriptivephysadj ::=
             non-gaussian | simple | inflationary | <inflationadj> inflationary
           | exactly-soluble | unified | minimal | quantum | linear | nonlinear
           | gravitational | quantum gravitational | cosmological | supersymmetric
           | holographic | entropic | alternative | nonstandard | multidimensional
           | nonlocal | chiral | phenomenological | nonperturbative | perturbative
           | warped | <n>-dimensional | conformal | modified | supergravity mediated
           | gauge mediated | anomaly mediated | superconformal | extra-ordinary
           | general | anthropic | nilpotent | asymmetric | <symmetry> symmetric
           | <symmetry> invariant | spontaneous | thermodynamic | planar | inertial
           | metastable | unstable | stable | tachyonic | transverse | longitudinal
           | momentum-dependent | exclusive | diffractive | dynamical | effective
           | acoustic | primordial | possible | impossible | calculable | predictive
           | unconventional | macroscopic | microscopic | holomorphic
           | consistent | inconsistent | anomalous | hadronic | leptonic
           | ferromagnetic | singular | nonsingular | leading | subleading
           | higher-order | novel | next-to-leading

inflationadj ::= <inflationadj> <inflationadj> | <inflationadj> <inflationadj>
               | <inflationadj> <inflationadj> | <inflationadj> <inflationadj>
               | $D$-Term | anisotropic | asymptotic | brane | braneworld chaotic
               | Brans-Dicke | chaotic | cosmological | de Sitter | double
               | dynamical | elastic | extended | extranatural | F-term | hybrid | false vacuum
               | first-order | general | generalized assisted | higher-curvature | hyper
               | inflatonless | inspired | inverted | K | large-scale | late-time
               | mild | low scale | modular invariant | multi-component | multi-field stochastic
               | multi-field | mutated | natural | new | $\Omega<1$ | assisted | brane-assisted
               | tachyonic | liouville | open | Cobe-Dmr-normalized | D-term | dissipative
               | supersymmetric | eternal | extended | extreme | facilitated | warm
               | generalized | gravitoelectromagnetic | holographic | induced | inhomogeneous
               | intermediate | kinetic | local | mass | moduli | slow-roll | multi-scalar
               | supergravity | natural | boundary | cosmic | dominated | early
               | exact | fake | field line | fresh | gravity driven | induced-gravity
               | intermediate scale | Jordan-Brans-Dicke | large field | locked
               | massive | monopole | multiple | multiple-stage | supergravity
               | non-slow-roll | old | particle physics | pole-like | power-law mass
               | precise | pseudonatural | quasi-open | racetrack | running-mass
               | simple | single scalar | single-bubble | spacetime | noncommutative
               | standard | steady-state | successful | sunergistic | tensor field
               | thermal brane | tilted ghost | topological | tsunami | unified | weak scale
               | noise-induced | one-bubble | open-universe | patch | polynomial | primary
               | quadratic | quintessential | rapid | asymmetric | scalar-tensor
               | non-canonical | smooth | spin-driven | Starobinsky | stochastic
               | string-forming | TeV-scale | three form | topological defect | viable
               | weak-dissipative | nonminimal | oscillating | phantom | power law
               | pre-big-bang | primordial | quantum | R-invariant | running
               | shear-free | rotating | slinky | spinodal | thermal | tidal | tree-level
               | two-stage | anthropic

######## Physicist ########

physicist ::= <physicistname> | <physicistname> | <physicistname>-<physicistname> 
physicistname ::=
              Weinberg | Feynman | Witten | Seiberg | Polchinski | Intrilligator
            | Vafa | Randall | Sundrum | Strominger | Georgi | Glashow | Coleman
            | Bohr | Fermi | Heisenberg | Maldacena | Einstein | Kachru | Arkani-Hamed
            | Schwinger | Higgs | Hitchin | Hawking | Stueckelberg | Unruh | Aranov-Bohm
            | 't Hooft | Silverstein | Horava | Lifschitz | Beckenstein | Planck
            | Euler | Lagrange | Maxwell | Boltzmann | Lorentz | Poincare | Susskind
            | Polyakov | Gell-Mann | Penrose | Dyson | Dirac | Argyres | Douglass
            | Gross | Politzer | Cabibo | Kobayashi | Denef | Shenker | Moore
            | Nekrosov | Gaiotto | Motl | Strassler | Klebanov | Nelson | Gubser
            | Verlinde | Bogoliubov | Schwartz

######## Concepts ########

mathconcept ::= <singmathconcept> | <pluralmathconcept>

singmathconcept ::= integrability | perturbation theory | localization
                  | duality | chaos | <mathadj> structure
                  | dimensionality | <dualtype>-duality | unitarity
                  | representation theory | Clebsch-Gordon decomposition
                  | sheaf cohomology | anomaly matching | semidefinite programming
                  | harmonic analysis | causality

pluralmathconcept ::= gerbs | path integrals | Feynman diagrams | <mathadj> structures
              | <physicist>'s equations | conformal blocks
              | <optype> operators | <dualtype>-dualities | <physicist> points | <group> characters
              | central charges | charges | currents | representations | <physicist> conditions
              | symplectic quotients | hyperkahler quotients | Nahm's equations | vortices
              | vortex equations | Hilbert schemes | integration cycles | divisors | line bundles
              | index theorems | flow equations | metrics | Gromov-Witten invariants
              | Gopakumar-Vafa invariants | Donaldson polynomials | type-1 factors
              | Donaldson-Witten invariants | automorphic forms | modular forms | quasimodular forms
              | integrable hierarchies

physconcept ::= <pluralphysconcept> | <singphysconcept>

pluralphysconcept ::=
                examples of <physconcept> | equations of <theory> | <n>-point correlators
              | correlators of <optype> operators
              | <symmetry> algebras | fragmentation functions | decay constants | anomaly constraints
              | anomalous dimensions | PDFs | observables | effects of <physconcept> | partition functions
              | <particle> collisions | <physadj> effects | <physadj> parameters | <physadj> hierarchies
              | <physconceptnoun> | <physadj> <physconceptnoun> | amplitudes | scattering amplitudes
              | geometric transitions | <optype> operators | bounds on <physconcept>
              | <physadj> processes | <physadj> events | large logarithms
              | Sudakov logs | soft theorems

physconceptnoun ::= sectors | vacua | solutions | states | geometries | currents
                  | backgrounds | wavefunctions | excitations | branching ratios | amplitudes
                  | decays | exotics | corrections | interactions | inhomogeneities
                  | correlation functions | form factors | S-matrix elements | matix elements
                  | remnants
              
singphysconcept ::= <symmviol> <symmetry> invariance | <symmviol> <symmetry> symmetry
                  | <symmetry> symmetry breaking | <mechanism> | confinement | the <physadj> limit
                  | the <physadj> law | the <symmetry> algebra | the beta function
                  | the Wilsonian effective action | the <n>PI effective action
                  | the partition function | <particle> production | black hole evaporation
                  | the effective potential | Hawking radiation
                  | renormalization | regularization | backreaction | AdS/CFT | the partition function
                  | a <physadj> hierarchy | the <physicist> formalism | the <physadj> formalism
                  | <physadj> regularization | the 't Hooft anomaly matching condition
                  | the S-matrix | the <particle> S-matrix | the Hamiltonian | the Lagrangian | the omega deformation
                  | the <physadj> Hilbert space | the Hilbert space | "<singphysconcept>"
                  | <effect> | the OPE | IR behavior | UV behavior | a warped throat
                  | a holographic superconductor | the <particle> charge | the <particle> gyromagnetic ratio
                  | the HRT surface | the <formulaname> formula | a <extended> <formulaname> formula
                  | the <boundname> bound | a <extended> <boundname> bound
                  | bit threads | Tomita-Takesaki theory | the <physicist> equation
                  | the <energycondition> | the <bootstraptype> bootstrap
                  | the entangling surface | the <limittype> limit | analyticity in spin
                  | the crossing equation | <entropytype> | the <entropytype> of <singspace>
                  | complexity | data from <experiment> | the BV formalism | the shadow formalism
                  | the space of <optype> operators | the OPE of <optype> operators
                  | the equivalence principle | gravitational birefringence
                  | a bound on <physconcept> | chaos | the <horizontype> horizon
                  | <moonshinetype> moonshine | <particle> physics | soft radiation
                  | <particle> mixing | <optype> operator mixing | the <physicist> instanton
                  | the Weyl anomaly

entropytype ::= von-Neumann entropy | mutual information | relative entropy | complexity | entanglement
              | entanglement entropy | entanglement negativity | holographic mutual information

extended ::= extended | improved | less-useful | more-useful | modified | generalized | defect | boundary

formulaname ::= Ryu-Takayanagi | CHY | Duistermaat-Heckman | Cardy | Lorentzian inversion | Froissart-Gribov
boundname ::= Beckenstein | HKS | Froissart | chaos 

bootstraptype ::= conformal | numerical | analytical | lightcone | Regge | modular | sphere-packing
                | S-matrix | cosmological

energycondition ::= Average Null Energy Condition | ANEC | Quantum Null Energy Condition | QNEC
                  | Quantum Focusing Conjecture | Null Energy Condition | Dominant Energy Condition
                  | Generalized Second Law 

moonshinetype ::= monstrous | umbral | Mathieu | <group>

limittype ::= lightcone | Regge | OPE | second-sheet lightcone | multi-Regge | high-energy | low-energy
            | small-angle | long-distance
                  
dualtype ::=T|U|S|magnetic|electric|gravitational|boundary|Seiberg|Geometric Langlands|N=<n>|holographic

symmviol ::= | violation of | <physadj> violation of | breaking of
symmetry ::= dilation | translation | rotation | Lorentz | conformal | superconformal
           | super | Poincare | worldsheet | diffeomorphism | superdiffeomorphism | <liegroup>
           | dual-superconformal | Yangian | Virosoro | <nzdigit>-form | higher-form
           | broken <symmetry> | unbroken <symmetry> | spontaneously-broken <symmetry>

mechanism ::= the <mechanismadj> mechanism | the <physadj> <mechanismadj> mechanism
mechanismadj ::= Higgs | seesaw | <physicist> | attractor | anomaly inflow | reheating
               | SuperHiggs | confinement

effect ::= the <effectadj> effect | the <physadj> <effectadj> effect | <physadj> effects
effectadj ::= <physicist> | quantum Hall | Unruh | Stark | Casimir

######## Subject ########

physsubject ::= <singphyssubject> | <pluralphyssubject>
singphyssubject ::=
            quintessence | <inflationadj> inflation | inflation | dark matter | dark energy
          | spacetime foam | instanton gas | <entropytype> | flavor | bubble nucleation

pluralphyssubject ::=
            condensates | <branetype> branes | cosmic rays | instanton liquids
          | <physadj> fluctuations | bubbles | tensor networks

particle ::= hadron| lepton| quark| neutrino| electron| positron| WIMP|
             slepton| squark| kk graviton| gluon| W-boson| Z-boson| neutralino|
             chargino| ghost| axion| monopole| soliton| dion| kaon| B-meson| pion|
             heavy-ion| Higgs| <particleadj> <particle>| <particleadj> <particle>|
             <particleadj> particle| anyon| magnon

particleadj ::= high-energy | soft | prompt | heavy | light | higher-spin | long-lived
              | unstable | stable | relativistic

subject ::= <singsubject> | <pluralsubject>

pluralsubject ::= <pluralmodel> | <pluraltheoryobj> | <particle>s
          | <pluralphysconcept> in <modeltheory>
          | <pluralmathconcept> in <theory> | <mathadj> <pluralmathconcept>
          | <pluralphysconcept> | <pluraltheory> | <pluralphyssubject> <objectplace>
          | <pluraltheoryobj> <objectplace> | some <specific> <examples> of <subject>
          | <pluralmathconcept> on <space>

specific ::= specific | general | little-known | novel
examples ::= cases | examples | investigations | computations | frameworks | paradigms

singsubject ::= <singmodel> | <singtheory> | <singtheoryobj> | <problem>
              | <solution> | <studyingverb> <modeltheory>
              | <article> <physadj> <actiondone> of <modeltheory> | <singphysconcept> in <modeltheory>
              | <singmathconcept> in <theory> | <mathadj> <singmathconcept>
              | <singphysconcept> | the <actiondone> of <modeltheory>
              | <article> <actiondone> of <mathconcept> in <modeltheory>
              | the <correspondent>/<correspondent> correspondence
              | <article> <dualtype>-dual of <modeltheory> | <dualtype>-duality in <modeltheory>
              | <singtheoryobj> <objectplace> | <singphyssubject> <objectplace>
              | <singsubject> (<including> <subject>) | <singmathconcept> on <space>
              | <singmathconcept> | a certain notion of <singmathconcept>
              | a <test> of <singsubject> | a <test> of <singsubject> <via> <subject>

modeltheory ::= <model> | <theory>

including ::= including | excluding | involving | taking into account

correspondent ::= <generalmodel> | <propermodel> | <properqft> | <genericqft> | <mathspace>

solution ::= <article> solution <solved> | <article> <soladj> solution <solved>
           | <article> solution <solved> <via> <subject>
           | <article> <soladj> solution <solved> <via> <subject>
           | a resolution of <problem> | a <soladj> resolution of <problem>
           | a <soladj> approach to <problem>
solved ::= to <problem> | of <theory>
via ::= via | through | from | by
soladj ::= better | new | beautiful | quantum | physical | old | clever
         | minimal | non-minimal | <physadj> | anthropic | entropic | possible
         | probable | partial

problem ::= the <problemtype> problem 
problemtype ::= hierarchy | flavor | cosmological constant | lithium | mu
              | strong CP | naturalness | little hierarchy | SUSY CP | LHC inverse
              | cosmic coincidence | U(1) | fine-tuning | mu/B_mu | confinement
              | black-hole information

######## Verbs ########

verb ::= derive | obtain | deduce | discover | find
       | conjecture | check | calculate | predict
verbed ::= derived | obtained | deduced | discovered | found | conjectured
         | realized | checked | calculated | predicted

studyverb ::= study | solve | investigate | demystify | bound
            | classify | obtain | derive | generalize | explore
            | examine | consider | analyze | evaluate | review
            | survey | explain | clarify | shed light on
            | extend | construct | reconstruct | calculate | discuss
            | formulate | reformulate | understand

studyingverb ::= studying | solving | investigating | demystifying | bounding
               | classifying | obtaining | deriving | generalizing | exploring
               | examining | considering | analyzing | evaluating | reviewing
               | surveying | explaining | clarifying | formulating | reformulating
               | extending | constructing | reconstructing | discussing | understanding

studiedverb ::= studied | solved | investigated | demystified | bounded
              | classified | obtained | derived | generalized | explored
              | examined | considered | analyzed | evaluated | reviewed
              | surveyed | recalled | explained | clarified | extended | constructed
              | reconstructed | discussed | understood

singbeingverb ::= exists | is present | must be there | must be present | does not exist

revealed ::= revealed | produced | led to | led us to | exposed | uncovered

singstatementverb ::= is | is equivalent to | is related to | derives from
                      | reduces to | follows from | lets us <studyverb>
                      | can be interpreted as | can be <verbed> from | turns out to be equivalent to
                      | relates to | depends on | <adverb> <singstatementverb>
                      | can be incorporated into | can be brought to bear in <studyingverb>
                      | is useful for <studyingverb> | is the final component in <studyingverb>

pluralstatementverb ::= are the same as | are equivalent to | are related to
                      | let us <studyverb> | can compute | follow from | can be interpreted as
                      | can be <verbed> from | turn out to be equivalent to | relate to | depend on
                      | derive from | reduce to | <adverb> <pluralstatementverb>
                      | can be incorporated into | can be brought to bear in <studyingverb>
                      | are useful for <studyingverb> | relate <subject> to

yields ::= yields | gives | provides | produces | gives rise to
prove ::= prove | show | demonstrate | establish | illustrate | determine | confirm | verify

contradict ::= contradict | disagree with | agree with | find inconsistencies with | argue against
             | run counter to | cannot corroborate | cannot support | challenge | fail to <prove>

######## Language ########

capital ::= A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z

article ::= a | the

adverb ::= remarkably | actually | interestingly | however | moreover | therefore | thus
         | consequently | curiously | fortunately | unfortunately | surprisingly | unsurprisingly
         | quite simply | in short 

recently ::= recently | in recent years | in recent papers | over the last decade
           | in the 20th century | among particle physicists | among mathematicians

thereby ::= <thereby> <thereby> | thereby | completely | conclusively | wholly
          | thoroughly | fully | ultimately | unambiguously

motivated ::= motivated by this | inspired by this | continuing in this vein
            | continuing with this program

assuming ::= if | whenever | provided that | supposing that | assuming | assuming that
           | as long as | given that

preposition ::= after | before | while | when
whenphrase ::= <preposition> <studyingverb> <subject> 

actiondone ::= reduction | compactification | formulation | extension | solution | analytic continuation

qualifier ::= at least in the context of <subject> | without regard to <subject>
            | in the approximation that <statement> | in the limit that <statement>
            | as realized in <subject> | as hinted at by <physicist>
            | as revealed by <mathconcept> | by <symmetry> symmetry | by symmetry
            | whenever <statement> | as we will see in this paper | with the help of <subject>
            | as will be made clear | as will be <studiedverb> shortly | in the <singmathconcept> case

inorderto ::= to <prove> that <statement> | in order to <prove> that <statement>
            | in order to avoid <studyingverb> <subject> | to best <studyverb> <subject>
            | to <studyverb> <subject> | to <studyverb> recent results linking <subject> and <subject>
            | in a way that <yields> <subject> | to explore questions such as the <singmathconcept> conjecture

was ::= has been | was
muchwork ::= much work <was> done | interesting progress <was> made
           | substantial progress has been made | minimal progress <was> made
           | some work <was> done | little work <was> done | a fair amount of work <was> done
           | partial progress <was> made

test ::= <computation> | test | probe | measurement | check
computation ::= computation | calculation | determination | prediction | analytical <test> | numerical <test>
correspondence ::= correspondence | conjecture | theorem | result
fact ::= fact | truth | principle | law | theorem | rule | pattern
       | structure | framework | edifice

thesame ::= the same | the very same | our very same | our | the exact same | a previously studied

beautiful ::= beautiful | surprising | elegant | pretty | arresting | charming
            | simple | ingenious | sophisticated | intricate | elaborate | detailed
            | confusing | bewildering | perplexing | elaborate | involved | complicated
            | startling | unforseen | amazing | extraordinary | remarkable
            | shocking | unexpected | deep | mysterious | profound | unsurprising
            | essential | fundamental | crucial | critical | key | important

######## Statements & Sentences ########

statement ::= <singsubject> <singstatementverb> <singsubject> | <pluralsubject> <pluralstatementverb> <subject>
            | <singsubject> is <descriptivephysadj> | <pluralsubject> are <descriptivephysadj>

asentence ::= 
    <asentence>, <qualifier>
  | <recently>, <muchwork> on <model>
  | <recently>, <muchwork> <studyingverb> <theory>
  | <recently>, <muchwork> on <model> <inorderto>
  | <recently>, <muchwork> <studyingverb> <theory> <inorderto>
  | <muchwork> <recently> on <model>
  | <muchwork> <recently> <studyingverb> <theory>
  | <recently>, work on <model> has opened up a <descriptivephysadj> class of <physadj> models
  | <recently>, <physicistname> <studiedverb> <subject>
  | <recently>, <physicistname> <verbed> that <statement>
  | <asentence>. we take a <descriptivephysadj> approach
  | <asentence>. <motivated>, <bsentence>
  | <singsubject> offers the possibility of <studyingverb> <subject>
  | <singsubject> <yields> a <beautiful> framework for <studyingverb> <subject>
  | <singsubject> is usually <verbed> <via> <subject>
  | <pluralsubject> are usually <verbed> <via> <subject>

bsentence ::=
    <bsentence>, <qualifier>
  | <inorderto>, <bsentence>
  | we <studyverb> <subject>
  | we solve <problem>
  | we take a <descriptivephysadj> approach to <subject>
  | we <prove> that <statement>
  | we <prove> a <beautiful> correspondence between <subject> and <subject>
  | <bsentence>, and <studyverb> <subject>
  | <bsentence>, and <verb> that <statement>
  | <bsentence>, and <verb> that, <qualifier>, <statement>
  | <bsentence>, <thereby> <studyingverb> that <statement>
  | <via> <studyingverb> <pluralmathconcept>, we <studyverb> <subject>
  | <via> <studyingverb> <physconcept>, we <studyverb> <subject>
  | we <verb> evidence for <subject>
  | using the behavior of <singsubject>, we <studyverb> <subject>
  | we present a criterion for <subject>
  | we make contact with <subject>, <adverb> <studyingverb> <subject>
  | we make contact between <subject> and <subject>
  | we <studyverb> why <statement>
  | we use <subject> to <studyverb> <subject>
  | we use <subject>, together with <subject> to <studyverb> <subject>
  | in this paper, <bsentence>

csentence ::=
    <csentence>, <qualifier>
  | <motivated>, <bsentence>
  | we take a <descriptivephysadj> approach
  | <adverb>, <statement>
  | next, <bsentence>
  | <singtheory> is also <studiedverb>
  | <pluraltheory> are also <studiedverb>
  | <singmodel> is also <studiedverb>
  | <pluralmodel> are also <studiedverb>
  | <singphysconcept> is also <studiedverb>
  | <pluralphysconcept> are also <studiedverb>
  | we <thereby> <prove> a <beautiful> correspondence between <subject> and <subject>
  | we also <verb> agreement with <subject>
  | the <computation> of <physconcept> localizes to <space>
  | <statement> <assuming> <statement>
  | <subject> <revealed> a <beautiful> <fact>: <statement>
  | <studyingverb> is made easier by <studyingverb> <subject>
  | our <computation> of <subject> <yields> <subject>
  | as an interesting outcome of this work for <subject>, <bsentence>
  | <csentence>, <studyingverb> <subject>
  | <adverb>, <singsubject> <singstatementverb> <thesame> <singmathconcept>
  | we therefore <contradict> a result of <physicistname> that <statement>
  | this probably <singstatementverb> <subject>, though we've been unable to <prove> a <correspondence>
  | this is most likely a result of <physsubject>, an observation first mentioned in work on <subject>
  | this <yields> an extremely precise <test> of <singphysconcept>
  | the <singmathconcept> depends, <adverb>, on whether <statement>
  | a <beautiful> part of this analysis <singstatementverb> <subject>
  | in this <correspondence>, <singsubject> makes a <beautiful> appearance
  | why this happens can be <studiedverb> by <studyingverb> <subject>
  | the title of this article refers to <subject>
  | we <verb> that <singtheoryobj> <singbeingverb> <qualifier>
  | this <correspondence> has long been understood in terms of <subject>

dsentence ::=
    <dsentence>, <qualifier>
  | <whenphrase>, we <verb> that <statement> | <statement>
  | <whenphrase>, we <verb> that, <qualifier>, <statement>
  | <dsentence>. <adverb>, <dsentence> | our results <prove> that <statement>

closing ::= finally, <bsentence>
          | <adverb>, there is much to be done
          | we hope this paper provides a good starting point for <studyingverb> <subject>
          | we leave the rest for future study
          | <adverb>, <singsubject> is beyond the scope of this paper
          | we will provide more details in a future paper
          | our results are similar to work done by <physicistname>
          | we believe this is indicative of a <beautiful> <fact>
          | given this, our work may seem quite <beautiful>
             
abstract ::= 
    <asentence>. <bsentence>. <csentence>. <dsentence>.
  | <asentence>. <adverb>, <asentence>. <bsentence>. <csentence>. <dsentence>.
  | <asentence>. <bsentence>. <csentence>. <dsentence>. <closing>.
  | <asentence>. <adverb>, <asentence>. <bsentence>. <csentence>. <dsentence>. <closing>.
  | <statement>. <csentence>. <csentence>. <dsentence>.
  | <statement>. <adverb>, <asentence>. <csentence>. <csentence>. <dsentence>.
  | <statement>. <adverb>, <asentence>. <csentence>. <csentence>. <dsentence>. <closing>.
  | <bsentence>. <csentence>. <adverb>, <asentence>. <csentence>. <csentence>. <dsentence>.
  | <bsentence>. <csentence>. <dsentence>. <adverb>, <asentence>. <csentence>. <closing>.

title ::= <subject> | <fancytitle> | <fancytitle>
fancytitle ::= <subject> and <subject> | <subject> and <subject>
             | <subject> and <subject>
             | from <subject> to <subject>
             | <subject> <verbed> <via> <pluralmathconcept> | towards <subject>
             | <subject> <via> <subject> | <subject> as <subject>
             | <studyingverb> <subject> | <studyingverb> <subject>: <subject>
             | <soladj> approaches to <problem>
             | why <pluralsubject> are <descriptivephysadj>
             | <studyingverb> <subject>: a <descriptivephysadj> <approach>
             | on <subject> | progress in <subject>
             | <subject> revisited
             | <remarks> on <subject>
             | <subject> in <subject>

approach ::= approach | formalism | method | technique | procedure
remarks ::= remarks | comments

author ::= <capital>. <physicistname> | <capital>. <capital>. <physicistname>
authors ::= <author> | <author>, <authors>

morecomments ::= <smallinteger> figures | JHEP style | Latex file | no figures | BibTeX
               | JHEP3 | typos corrected | <nzdigit> tables | added refs | minor changes
               | minor corrections | published in PRD | reference added | pdflatex
               | based on a talk given on <physicistname>'s <nzdigit>0th birthday
               | talk presented at the international <pluralphysconcept> workshop
comments ::= <smallinteger> pages | <comments>, <morecomments>

primarysubj ::= High Energy Physics - Theory (hep-th)| High Energy Physics - Phenomenology (hep-ph)
secondarysubj ::= Nuclear Theory (nucl-th)| Cosmology and Extragalactic Astrophysics (astro-ph.CO)|
             General Relativity and Quantum Cosmology (gr-qc)| Statistical Mechanics (cond-mat.stat-mech)
papersubjects ::= <primarysubj> | <papersubjects>; <secondarysubj>

paper ::= <title> \\ <authors> \\ <comments> \\ <papersubjects> \\ <abstract>

tweetpaper ::= <authors>: <title>
